// Solutions to the n-queens problem are represented as lists of the form
//    [x_1,x_2, ..., x_n]
// where x_i in {1, ..., n} and x_i specifies, that in the ith column the 
// queen is positioned in row x_i.  This representation automatically ensures
// that there is exactly one queen per column.
// 
// For example, the list [1, 5, 8, 6, 3, 7, 2, 4] represents the following
// solution to the 8-queens problem.  In the diagramm below, the rows are
// numbered form top to button, so the topmost row is numbered 1, while the
// row at the bottom is numbered 8.
//
//     -----------------------------------------------------------------
//     |       |       |       |       |       |       |       |       |
//     |   Q   |       |       |       |       |       |       |       |
//     |       |       |       |       |       |       |       |       |
//     -----------------------------------------------------------------
//     |       |       |       |       |       |       |       |       |
//     |       |       |       |       |       |       |   Q   |       |
//     |       |       |       |       |       |       |       |       |
//     -----------------------------------------------------------------
//     |       |       |       |       |       |       |       |       |
//     |       |       |       |       |   Q   |       |       |       |
//     |       |       |       |       |       |       |       |       |
//     -----------------------------------------------------------------
//     |       |       |       |       |       |       |       |       |
//     |       |       |       |       |       |       |       |   Q   |
//     |       |       |       |       |       |       |       |       |
//     -----------------------------------------------------------------
//     |       |       |       |       |       |       |       |       |
//     |       |   Q   |       |       |       |       |       |       |
//     |       |       |       |       |       |       |       |       |
//     -----------------------------------------------------------------
//     |       |       |       |       |       |       |       |       |
//     |       |       |       |   Q   |       |       |       |       |
//     |       |       |       |       |       |       |       |       |
//     -----------------------------------------------------------------
//     |       |       |       |       |       |       |       |       |
//     |       |       |       |       |       |   Q   |       |       |
//     |       |       |       |       |       |       |       |       |
//     -----------------------------------------------------------------
//     |       |       |       |       |       |       |       |       |
//     |       |       |   Q   |       |       |       |       |       |
//     |       |       |       |       |       |       |       |       |
//     -----------------------------------------------------------------
//

allSolutions := procedure(n) {
    all := {};
    check {
        solve([], n, all);
    }
    return all;
};

// The interpretation of the parameters is as follows:
//      queens: list of those queens that have already been placed
//      n:      size of the board
solve := procedure(queens, n, rw all) { 
    if (#queens == n) {
        all += { queens };
        backtrack;
    }
    for (x in {1 .. n} - {i : i in queens}) {
        check {
            testNext(queens, x);
            // if this has worked so far, try to add more queens
            solve(queens + [x], n, all);
        } 
    }
    backtrack;
};

// Check whether it is safe to add a queen in row x in the column that
// follows those columns that are already specified in the list queens of
// queens already placed.  It is assumed that the queens already 
// placed cannot attack each other.
testNext := procedure(queens, x) {
    m := #queens;
    if (exists (i in {1 .. m} | i-queens[i] == m+1-x || i+queens[i] == m+1+x)) {
        backtrack;
    }
};

s := allSolutions(8);
print(#s);
for (l in s) {
    print(l);
}
