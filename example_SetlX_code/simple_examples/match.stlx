// this program demonstrates the match statement

exprString := "55 * 99 - 123 ** (14 - 2)";
exprTerm   := parse(exprString);

print(canonical(exprTerm));

match (exprTerm) {
    case a - b ** (c) :
        print("match: a = $a$, b = $b$, c = $c$");
    default :
        print("failed to match");
}

print();

exprString := "[1, 99, 7]";
exprTerm   := parse(exprString);

print(canonical(exprTerm));

match (exprTerm) {
    case [d, e, f] :
        print("match: d = $d$, e = $e$, f = $f$");
    default :
        print("failed to match");
}

print();

list    := [1, "foo", 7];

print(canonical(list));

match (list) {
    case [g, "foo", h] :
        print("match: g = $g$, h = $h$");
    default :
        print("failed to match");
}

print();

list    := [1, "foo", 7];

print(canonical(list));

match (list) {
    case [i, "bar", j] : // MUST NOT match
        print("match: i = $i$, j = $j$");
    default :
        print("failed to match");
}

print();

exprString := "sqrt(a**2 + b**2)";
exprTerm   := parse(exprString);

print(canonical(exprTerm));

match (exprTerm) {
    case fnc(u + _ ** v)                // because the function name is a variable, every function call with fitting arguments matches
         | fnc == @@@variable("sqrt"):  // thats why we further restrict it to match only the name of the sqrt function
        print("match: fnc = $fnc$, u = $u$, v = $v$");
    default :
        print("failed to match");
}

print();

exprString := "sqrt(a**2 + b**2) == c";
exprTerm   := parse(exprString);

print(canonical(exprTerm));

match (exprTerm) {
    case fnc(x + _ ** y) == _ | evalTerm(fnc) == sqrt :  // match the function call only if it is exactly sqrt
        print("match: sqrt: x = $x$, y = $y$");
    default :
        print("failed to match");
}

print();

exprString := "u";
exprTerm   := parse(exprString);

print(canonical(exprTerm));

match (exprTerm) {
    case z | isVariable(z) :
        print("match: z = $z$");
    default :
        print("failed to match");
}

print();

// it is also possible to use condition in match!

exprString := "1 + 2 * 3";
exprTerm   := parse(exprString);

print(canonical(exprTerm));

match (exprTerm) {
    case j + k * l | j == l : // MUST NOT match, because 1 != 3
        print("match (a): j = $j$, k = $k$, l = $l$");
    case j + k * l | j + k == l:
        print("match (b): j = $j$, k = $k$, l = $l$");
    default :
        print("failed to match");
}

print();

